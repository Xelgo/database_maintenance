#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ТипБазыПриИзмененииНаСервере();
	СпособОбновленияПриИзмененииНаСервере();
	АктуализацияПаролей();

КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПарольИзмененХранилище Тогда
		УстановитьПривилегированныйРежим(Истина);
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(ТекущийОбъект.Ссылка, ПарольХранилище, "ПарольХранилище");
		УстановитьПривилегированныйРежим(Ложь);
		ПарольХранилище = ?(ЗначениеЗаполнено(ПарольХранилище), УникальныйИдентификатор, "");
		ПарольИзмененХранилище = Ложь;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
&НаКлиенте
Процедура ТипБазыПриИзменении(Элемент)
	ТипБазыПриИзмененииНаСервере();
КонецПроцедуры
&НаКлиенте
Процедура СпособОбновленияПриИзменении(Элемент)
	СпособОбновленияПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПарольХранилищеПриИзменении(Элемент)

	ПарольИзмененХранилище = Истина;

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура АктуализацияПаролей()

	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьПривилегированныйРежим(Истина);
		ПарольХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Объект.Ссылка, "ПарольХранилище");
		УстановитьПривилегированныйРежим(Ложь);
		ПарольХранилище = ?(ЗначениеЗаполнено(ПарольХранилище), УникальныйИдентификатор, "");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура СпособОбновленияПриИзмененииНаСервере()

	ЭтоОбновлениеИзCf = Объект.СпособОбновления = Перечисления.ММ_СпособыОбновления.ИзФайлаCf;
	ЭтоОбновлениеИзХранилища = Объект.СпособОбновления = Перечисления.ММ_СпособыОбновления.ИзХранилищаКонфигурации;
	ЭтоТиповыеОбновления = Объект.СпособОбновления = Перечисления.ММ_СпособыОбновления.ТиповыеОбновления;
    // ToDo ЭтоТиповыеОбновления.
    
	Элементы.ОбновляемсяИзХранилища.Видимость = ЭтоОбновлениеИзХранилища;
	Элементы.ОбновляемсяИзCf.Видимость = ЭтоОбновлениеИзCf;
    
КонецПроцедуры

&НаСервере
Процедура ТипБазыПриИзмененииНаСервере()

	ЭтоФайловаяБаза = Объект.ТипБазы = Перечисления.ММ_ТипБазы.Файловая;
	ЭтоСервернаяБаза = Объект.ТипБазы = Перечисления.ММ_ТипБазы.Серверная;

	Элементы.НастройкиФайловойБазы.Видимость = ЭтоФайловаяБаза;
	Элементы.НастройкиСервернойБазы.Видимость = ЭтоСервернаяБаза;

	Если ЭтоФайловаяБаза Тогда
		Объект.Владелец = Справочники.ММ_Кластер1С.ФайловаяБаза;
	КонецЕсли;
	Если ЭтоСервернаяБаза Тогда
		Если Объект.Владелец = Справочники.ММ_Кластер1С.ФайловаяБаза Тогда
			Объект.Владелец = Неопределено;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти