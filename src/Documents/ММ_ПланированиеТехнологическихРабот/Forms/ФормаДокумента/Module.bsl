#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МинутПростоя = Константы.ММ_КоличествоМинутСтандартногоТехнологическогоПростоя.Получить();
	ПланируемаяРаботаПриИзмененииНаСервере();

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.УведомитьПользователей = Истина;
		Объект.АвтоматизацияОбновления = Истина;
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Элементы.ШаблонУведомления.Видимость = Объект.УведомитьПользователей;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УведомитьПользователейПриИзменении(Элемент)
	Элементы.ШаблонУведомления.Видимость = Объект.УведомитьПользователей;
КонецПроцедуры
&НаКлиенте
Процедура ПланируемаяРаботаПриИзменении(Элемент)
	ПланируемаяРаботаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновляемаяБазаПриИзменении(Элемент)
	ОбновляемаяБазаПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачалаРаботПриИзменении(Элемент)
	КоличествоСекунд = 60;
	Объект.ДатаОкончанияРабот = Объект.ДатаНачалаРабот + МинутПростоя
		* КоличествоСекунд;
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
&НаСервере
Процедура ПланируемаяРаботаПриИзмененииНаСервере()
	ЭтоОбновление = Объект.ВидШаблона = Перечисления.ММ_ВидыШаблоновОтправкиСообщений.ОбновлениеБазы;

	Элементы.ОбновляемаяБаза.Видимость = ЭтоОбновление;
	Элементы.СписокОбновляемыхРасширений.Видимость = ЭтоОбновление;
	Элементы.АвтоматизацияОбновления.Видимость = ЭтоОбновление;
	Элементы.База.Видимость = ЭтоОбновление;

КонецПроцедуры

&НаСервере
Процедура ОбновляемаяБазаПриИзмененииНаСервере()

	Запрос = Новый Запрос("ВЫБРАТЬ
						  |	ММ_Расширения.Ссылка Как Расширение,
						  |	ММ_Расширения.СпособОбновления,
						  |	ИСТИНА КАК Обновлять
						  |ИЗ
						  |	Справочник.ММ_Расширения КАК ММ_Расширения
						  |ГДЕ
						  |	ММ_Расширения.Владелец = &Владелец
						  |	И НЕ ММ_Расширения.ПометкаУдаления");
	Запрос.УстановитьПараметр("Владелец", Объект.ОбновляемаяБаза);
	Объект.СписокОбновляемыхРасширений.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры

#КонецОбласти